-- programs ------------------------------------------------
entrypoints Program ;

Program.   	Program 	::= [TopDef] ;

TopClassDef. 	TopDef 		::= ClassDef;
TopFunDef.    	TopDef 		::= FnDef;

FnDef.	   	FnDef 		::= Type LIdent "(" [Arg] ")" Block ;
Arg. 	   	Arg 		::= NonVoidType LIdent;

ClassField.     ClassMember     ::= Type [LIdent] ";";
ClassMethod.    ClassMember     ::= FnDef;
ClassBlock.     ClassBlock      ::= "{" [ClassMember] "}" ;

Class.        	ClassDef 	::= "class" UIdent ClassBlock ;
ClassInh.     	ClassDef 	::= "class" UIdent "extends" UIdent ClassBlock ;

separator Arg "," ;
separator nonempty TopDef "" ;
separator nonempty ClassMember "" ;

-- statements ----------------------------------------------

Block.     Block 	::= "{" [Stmt] "}" ;

Empty.     Stmt 	::= ";" ;

BStmt.     Stmt 	::= Block ;
Decl.      Stmt 	::= NonVoidType [Item] ";" ;

NoInit.    Item 	::= LIdent ;
Init.      Item 	::= LIdent "=" Expr ;

Ass.       Stmt 	::= Expr "=" Expr  ";" ;
Incr.      Stmt 	::= LIdent "++"  ";" ;
Decr.      Stmt 	::= LIdent "--"  ";" ;

Ret.       Stmt 	::= "return" Expr ";" ;
VRet.      Stmt 	::= "return" ";" ;

Cond.      Stmt 	::= "if" "(" Expr ")" Stmt  ;
CondElse.  Stmt 	::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt 	::= "while" "(" Expr ")" Stmt ;
For.       Stmt 	::= "for" "(" NonVoidType LIdent ":" Expr ")" Stmt ;

SExp.      Stmt 	::= Expr  ";" ;

separator  Stmt "" ;
separator nonempty Item "," ;
separator nonempty LIdent "," ;

-- Types ---------------------------------------------------

ClassType.      ScalarType      ::= UIdent ;
Int.            ScalarType 	::= "int" ;
Str.            ScalarType 	::= "string" ;
Bool.           ScalarType 	::= "boolean" ;

ArrayType.      NonVoidType     ::= ScalarType "[]" ;
ScalarType.     NonVoidType     ::= ScalarType ;
NonVoidType.    Type            ::= NonVoidType ;
Void.           Type            ::= "void" ;

-- Expressions ---------------------------------------------

ENewObject.	Expr8 	::= "new" NonVoidType ;
ENewArray.	Expr7 	::= Expr8 "[" Expr "]" ;

EMember.	Expr6 	::= Expr6 "." LIdent ;
EMemberCall.    Expr6 	::= Expr6 "." LIdent "(" [Expr]")"  ;

EVar. 		Expr6 	::= LIdent ;
ELitInt. 	Expr6   ::= Integer ;
ELitTrue. 	Expr6   ::= "true" ;
ELitFalse. 	Expr6   ::= "false" ;
ENull. 		Expr6   ::= "null" ;
EString. 	Expr6   ::= String ;

EApp. 		Expr6   ::= LIdent "(" [Expr] ")" ;
EAccess. 	Expr6   ::= Expr6 "[" Expr "]";

-- ECast. 		Expr5   ::= "(" ")" Expr5 "as" CastType;
ECast. 		Expr5   ::= "(" UIdent ")" "null"  ;
Neg.            Expr5   ::= "-" Expr6 ;
Not. 		Expr5   ::= "!" Expr6 ;

EMul. 		Expr4   ::= Expr4 MulOp Expr5 ;
EAdd. 		Expr3   ::= Expr3 AddOp Expr4 ;
ERel. 		Expr2   ::= Expr2 RelOp Expr3 ;

EAnd. 		Expr1   ::= Expr2 "&&" Expr1 ;
EOr. 		Expr    ::= Expr1 "||" Expr ;

coercions Expr 8 ;
separator Expr "," ;

-- operators -----------------------------------------------

Plus.	AddOp   ::= "+" ;
Minus.	AddOp   ::= "-" ;
Times.	MulOp   ::= "*" ;
Div.	MulOp   ::= "/" ;
Mod.	MulOp   ::= "%" ;

LTH.	RelOp   ::= "<" ;
LE.	RelOp   ::= "<=" ;
GTH.	RelOp   ::= ">" ;
GE.	RelOp   ::= ">=" ;
EQU.	RelOp   ::= "==" ;
NE.	RelOp   ::= "!=" ;

-- comments ------------------------------------------------

comment "#" ;
comment "//" ;
comment "/*" "*/" ;

token UIdent (upper (letter | digit | '_')*) ;
token LIdent (lower (letter | digit | '_')*) ;
